From zoo Require Import
  prelude.
From zoo.common Require Import
  countable.
From zoo.iris.bi Require Import
  big_op.
From zoo.iris.base_logic Require Import
  lib.ghost_var
  lib.ghost_pred
  lib.ghost_list
  lib.oneshot
  lib.twins.
From zoo.language Require Import
  notations.
From zoo.diaframe Require Import
  diaframe.
From zoo_std Require Import
  option
  array
  atomic_array
  queue_3
  domain
  random_round.
From zoo_parabs Require Export
  base
  ws_queues_private__code.
From zoo_parabs Require Import
  ws_queues_private__types.
From zoo Require Import
  options.

Implicit Types l : location.
Implicit Types v t queue round : val.
Implicit Types o : option val.
Implicit Types vs ws : list val.
Implicit Types vss wss : list (list val).
Implicit Types status : status.
Implicit Types statuses : list status.

Class WsQueuesPrivateG Œ£ `{zoo_G : !ZooG Œ£} := {
  #[local] ws_queues_private_G_models_G :: GhostListG Œ£ (list val) ;
  #[local] ws_queues_private_G_owner_G :: TwinsG Œ£ (leibnizO status) ;
  #[local] ws_queues_private_G_channel_pred_G :: GhostPredG Œ£ (option val) ;
  #[local] ws_queues_private_G_channel_generation_G :: GhostVarG Œ£ (leibnizO gname) ;
  #[local] ws_queues_private_G_channel_state_G :: OneshotG Œ£ () (option val) ;
}.

Definition ws_queues_private_Œ£ := #[
  ghost_list_Œ£ (list val) ;
  twins_Œ£ (leibnizO status) ;
  ghost_pred_Œ£ (option val) ;
  ghost_var_Œ£ (leibnizO gname) ;
  oneshot_Œ£ () (option val)
].
#[global] Instance subG_ws_queues_private_Œ£ Œ£ `{zoo_G : !ZooG Œ£} :
  subG ws_queues_private_Œ£ Œ£ ‚Üí
  WsQueuesPrivateG Œ£.
Proof.
  solve_inG.
Qed.

#[local] Coercion status_to_val status : val :=
  match status with
  | Blocked =>
      ¬ßBlocked
  | Nonblocked =>
      ¬ßNonblocked
  end.

Inductive request :=
  | RequestBlocked
  | RequestNone
  | RequestSome (i : nat).
Implicit Types request : request.
Implicit Types requests : list request.

#[local] Definition request_to_val request : val :=
  match request with
  | RequestBlocked =>
      ¬ßRequestBlocked
  | RequestNone =>
      ¬ßRequestNone
  | RequestSome i =>
      ‚ÄòRequestSome( #i )
  end.

Inductive response :=
  | ResponseWaiting
  | ResponseNone
  | ResponseSome v.
Implicit Types response : response.
Implicit Types responses : list response.

#[local] Coercion option_to_response o :=
  match o with
  | None =>
      ResponseNone
  | Some v =>
      ResponseSome v
  end.
#[local] Definition response_to_val response : val :=
  match response with
  | ResponseWaiting =>
      ¬ßResponseWaiting
  | ResponseNone =>
      ¬ßResponseNone
  | ResponseSome v =>
      ‚ÄòResponseSome( v )
  end.

Section ws_queues_private_G.
  Context `{ws_queues_private_G : WsQueuesPrivateG Œ£}.

  Implicit Types Œ® : option val ‚Üí iProp Œ£.

  Record metadata := {
    metadata_queues_array : val ;
    metadata_queues : list val ;
    metadata_statuses_array : val ;
    metadata_requests_array : val ;
    metadata_responses_array : val ;
    metadata_inv : namespace ;
    metadata_size : nat ;
    metadata_models : gname ;
    metadata_owners : list gname ;
    metadata_channels : list (gname * gname) ;
  }.
  Implicit Types Œ≥ : metadata.
  Implicit Types Œ≥_owners : list gname.
  Implicit Types Œ≥_channels : list (gname * gname).

  #[local] Instance metadata_eq_dec : EqDecision metadata :=
    ltac:(solve_decision).
  #[local] Instance metadata_countable :
    Countable metadata.
  Proof.
    solve_countable.
  Qed.

  #[local] Definition models_auth' Œ≥_models sz vss : iProp Œ£ :=
    ghost_list_auth Œ≥_models vss ‚àó
    ‚åúlength vss = sz‚åù.
  #[local] Definition models_auth Œ≥ :=
    models_auth' Œ≥.(metadata_models) Œ≥.(metadata_size).
  #[local] Instance : CustomIpatFormat "models_auth" :=
    "(
      Hauth{_{}} &
      %Hvss{}
    )".
  #[local] Definition models_at' Œ≥_models i :=
    ghost_list_at Œ≥_models i (DfracOwn 1).
  #[local] Definition models_at Œ≥ :=
    models_at' Œ≥.(metadata_models).

  #[local] Definition owner‚ÇÅ' Œ≥_owners i status : iProp Œ£ :=
    ‚àÉ Œ≥_owner,
    ‚åúŒ≥_owners !! i = Some Œ≥_owner‚åù ‚àó
    twins_twin1 Œ≥_owner (DfracOwn 1) status.
  #[local] Definition owner‚ÇÅ Œ≥ :=
    owner‚ÇÅ' Œ≥.(metadata_owners).
  #[local] Instance : CustomIpatFormat "owner‚ÇÅ" :=
    "(
      %Œ≥_owner{_{}} &
      %Hlookup{_{}} &
      Htwin‚ÇÅ
    )".
  #[local] Definition owner‚ÇÇ' Œ≥_owners i status : iProp Œ£ :=
    ‚àÉ Œ≥_owner,
    ‚åúŒ≥_owners !! i = Some Œ≥_owner‚åù ‚àó
    twins_twin2 Œ≥_owner status.
  #[local] Definition owner‚ÇÇ Œ≥ :=
    owner‚ÇÇ' Œ≥.(metadata_owners).
  #[local] Instance : CustomIpatFormat "owner‚ÇÇ" :=
    "(
      %Œ≥_owner{_{}} &
      %Hlookup{_{}} &
      Htwin‚ÇÇ
    )".

  #[local] Definition channels_waiting' Œ≥_channels i : iProp Œ£ :=
    ‚àÉ Œ≥_channel gen,
    ‚åúŒ≥_channels !! i = Some Œ≥_channel‚åù ‚àó
    ghost_var Œ≥_channel.2 (DfracOwn (1/2)) gen ‚àó
    oneshot_pending gen (DfracOwn 1) ().
  #[local] Definition channels_waiting Œ≥ :=
    channels_waiting' Œ≥.(metadata_channels).
  #[local] Instance : CustomIpatFormat "channels_waiting" :=
    "(
      %Œ≥_channel_{} &
      %gen{} &
      %Hlookup_{} &
      Hgeneration_{} &
      Hpending_{}
    )".
  #[local] Definition channels_sender' Œ≥_channels i Œ® state : iProp Œ£ :=
    ‚àÉ Œ≥_channel,
    ‚åúŒ≥_channels !! i = Some Œ≥_channel‚åù ‚àó
    ghost_pred Œ≥_channel.1 (DfracOwn (3/4)) Œ® ‚àó
    match state with
    | None =>
        True
    | Some o =>
        ‚àÉ gen,
        ghost_var Œ≥_channel.2 (DfracOwn (1/2)) gen ‚àó
        oneshot_shot gen o
    end.
  #[local] Definition channels_sender Œ≥ :=
    channels_sender' Œ≥.(metadata_channels).
  #[local] Instance : CustomIpatFormat "channels_sender" :=
    "(
      %Œ≥_channel_{} &
      {>;}%Hlookup_{} &
      Hpred_{} &
      { {done}
        ( %gen{} &
          Hgeneration_{} &
          #Hshot_{}
        )
      ; _
      }
    )".
  #[local] Definition channels_receiver' Œ≥_channels i Œ® state : iProp Œ£ :=
    ‚àÉ Œ≥_channel gen,
    ‚åúŒ≥_channels !! i = Some Œ≥_channel‚åù ‚àó
    ghost_pred Œ≥_channel.1 (DfracOwn (1/4)) Œ® ‚àó
    ghost_var Œ≥_channel.2 (DfracOwn (1/2)) gen ‚àó
    match state with
    | None =>
        True
    | Some o =>
        oneshot_shot gen o
    end.
  #[local] Definition channels_receiver Œ≥ :=
    channels_receiver' Œ≥.(metadata_channels).
  #[local] Instance : CustomIpatFormat "channels_receiver" :=
    "(
      %Œ≥_channel_{} &
      %gen{} &
      %Hlookup_{} &
      Hpred_{} &
      Hgeneration_{} &
      {{done}#Hshot_{};_}
    )".

  #[local] Definition request_au Œ≥ i Œ® : iProp Œ£ :=
    AU <{
      ‚àÉ‚àÉ vss,
      models_auth Œ≥ vss
    }> @ ‚ä§ ‚àñ ‚ÜëŒ≥.(metadata_inv), ‚àÖ <{
      ‚àÄ‚àÄ o,
      match o with
      | None =>
          models_auth Œ≥ vss
      | Some v =>
          ‚àÉ vs,
          ‚åúvss !! i = Some (v :: vs)‚åù ‚àó
          models_auth Œ≥ (<[i := vs]> vss)
      end
    , COMM
      Œ® o
    }>.

  #[local] Definition request_model_blocked Œ≥ i : iProp Œ£ :=
    owner‚ÇÇ Œ≥ i Blocked.
  #[local] Instance : CustomIpatFormat "request_model_blocked" :=
    "{>;}Howner‚ÇÇ".
  #[local] Definition request_model_nonblocked' Œ≥ i j : iProp Œ£ :=
    ‚àÉ Œ®,
    ‚åúj < Œ≥.(metadata_size)‚åù ‚àó
    channels_sender Œ≥ j Œ® None ‚àó
    request_au Œ≥ i Œ®.
  #[local] Instance : CustomIpatFormat "request_model_nonblocked'" :=
    "(
      %Œß &
      {>;}% &
      Hchannels_sender &
      HŒß
    )".
  #[local] Definition request_model_nonblocked Œ≥ i j : iProp Œ£ :=
    owner‚ÇÇ Œ≥ i Nonblocked ‚àó
    request_model_nonblocked' Œ≥ i j.
  #[local] Instance : CustomIpatFormat "request_model_nonblocked" :=
    "(
      {>;}Howner‚ÇÇ &
      (:request_model_nonblocked' {/>/})
    )".
  #[local] Definition request_model Œ≥ i request : iProp Œ£ :=
    match request with
    | RequestSome j =>
          request_model_blocked Œ≥ i
        ‚à® request_model_nonblocked Œ≥ i j
    | _ =>
        owner‚ÇÇ Œ≥ i Nonblocked
    end.
  #[local] Instance : CustomIpatFormat "request_model" :=
    " [ (:request_model_blocked {/>/})
      | (:request_model_nonblocked {/>/})
      ]
    ".

  #[local] Definition response_model Œ≥ i response : iProp Œ£ :=
    match response with
    | ResponseWaiting =>
        channels_waiting Œ≥ i
    | ResponseNone =>
        ‚àÉ Œ®,
        channels_sender Œ≥ i Œ® (Some None) ‚àó
        Œ® None
    | ResponseSome v =>
        ‚àÉ Œ®,
        channels_sender Œ≥ i Œ® (Some $ Some v) ‚àó
        Œ® (Some v)
    end.
  #[local] Instance : CustomIpatFormat "response_model" :=
    "(
      %Œ®{} &
      Hchannels_sender{_{}} &
      HŒ®{}
    )".

  #[local] Definition inv_inner Œ≥ : iProp Œ£ :=
    ‚àÉ statuses requests responses,
    array_model Œ≥.(metadata_statuses_array) (DfracOwn 1) (status_to_val <$> statuses) ‚àó
    array_model Œ≥.(metadata_requests_array) (DfracOwn 1) (request_to_val <$> requests) ‚àó
    array_model Œ≥.(metadata_responses_array) (DfracOwn 1) (response_to_val <$> responses) ‚àó
    ([‚àó list] i ‚Ü¶ request ‚àà requests, request_model Œ≥ i request) ‚àó
    ([‚àó list] i ‚Ü¶ response ‚àà responses, response_model Œ≥ i response).

  #[local] Instance : CustomIpatFormat "inv_inner" :=
    "(
      %statuses{} &
      %requests{} &
      %responses{} &
      >Hstatuses_model &
      >Hrequests_model &
      >Hresponses_model &
      Hrequests &
      Hresponses
    )".
  Definition ws_queues_private_inv t Œπ (sz : nat) : iProp Œ£ :=
    ‚àÉ l Œ≥,
    ‚åút = #l‚åù ‚àó
    ‚åúŒπ = Œ≥.(metadata_inv)‚åù ‚àó
    ‚åúsz = Œ≥.(metadata_size)‚åù ‚àó
    meta l nroot Œ≥ ‚àó
    l.[size] ‚Ü¶‚ñ° #Œ≥.(metadata_size) ‚àó
    l.[queues] ‚Ü¶‚ñ° Œ≥.(metadata_queues_array) ‚àó
    ‚åúlength Œ≥.(metadata_queues) = Œ≥.(metadata_size)‚åù ‚àó
    array_model Œ≥.(metadata_queues_array) DfracDiscarded Œ≥.(metadata_queues) ‚àó
    l.[statuses] ‚Ü¶‚ñ° Œ≥.(metadata_statuses_array) ‚àó
    array_inv Œ≥.(metadata_statuses_array) Œ≥.(metadata_size) ‚àó
    l.[requests] ‚Ü¶‚ñ° Œ≥.(metadata_requests_array) ‚àó
    array_inv Œ≥.(metadata_requests_array) Œ≥.(metadata_size) ‚àó
    l.[responses] ‚Ü¶‚ñ° Œ≥.(metadata_responses_array) ‚àó
    array_inv Œ≥.(metadata_responses_array) Œ≥.(metadata_size) ‚àó
    inv Œπ (inv_inner Œ≥).
  #[local] Instance : CustomIpatFormat "inv" :=
    "(
      %l{} &
      %Œ≥{} &
      {%Ht_eq{};->} &
      {%HŒπ_eq{};->} &
      {%Hsz_eq{};->} &
      #Hmeta{_{}} &
      #Hl{}_size &
      #Hl{}_queues &
      %Hqueues{}_length &
      #Hqueues{}_model &
      #Hl{}_statuses &
      #Hstatuses{}_inv &
      #Hl{}_requests &
      #Hrequests{}_inv &
      #Hl{}_responses &
      #Hresponses{}_inv &
      #Hinv{}
    )".

  Definition ws_queues_private_model t vss : iProp Œ£ :=
    ‚àÉ l Œ≥,
    ‚åút = #l‚åù ‚àó
    meta l nroot Œ≥ ‚àó
    models_auth Œ≥ vss.
  #[local] Instance : CustomIpatFormat "model" :=
    "(
      %l{;_} &
      %Œ≥{;_} &
      %Heq{} &
      #Hmeta_{} &
      Hmodels_auth{_{}}
    )".

  Definition ws_queues_private_owner t i status ws : iProp Œ£ :=
    ‚àÉ l Œ≥ queue vs Œ®_sender Œ®_receiver,
    ‚åút = #l‚åù ‚àó
    meta l nroot Œ≥ ‚àó
    ‚åúŒ≥.(metadata_queues) !! i = Some queue‚åù ‚àó
    queue_3_model queue vs ‚àó
    models_at Œ≥ i vs ‚àó
    ‚åúvs `suffix_of` ws‚åù ‚àó
    owner‚ÇÅ Œ≥ i Nonblocked ‚àó
    channels_sender Œ≥ i Œ®_sender None ‚àó
    channels_receiver Œ≥ i Œ®_receiver None.
  #[local] Instance : CustomIpatFormat "owner" :=
    "(
      %l{;_} &
      %Œ≥{;_} &
      %queue{} &
      %vs{} &
      %Œ®_sender{_{}} &
      %Œ®_receiver{_{}} &
      %Heq{} &
      #Hmeta_{} &
      %Hqueues_lookup{_{}} &
      Hqueue_model{_{}} &
      Hmodels_at{_{}} &
      %Hws{} &
      Howner‚ÇÅ{_{}} &
      Hchannels_sender{_{}} &
      Hchannels_receiver{_{}}
    )".

  #[local] Instance owner‚ÇÇ_timeless Œ≥ i status :
    Timeless (owner‚ÇÇ Œ≥ i status).
  Proof.
    apply _.
  Qed.
  #[local] Instance channels_waiting_timeless Œ≥ i :
    Timeless (channels_waiting Œ≥ i).
  Proof.
    apply _.
  Qed.
  #[global] Instance ws_queues_private_model_timeless t vss :
    Timeless (ws_queues_private_model t vss).
  Proof.
    apply _.
  Qed.
  #[global] Instance ws_queues_private_inv_persistent t Œπ sz :
    Persistent (ws_queues_private_inv t Œπ sz).
  Proof.
    apply _.
  Qed.

  #[local] Lemma models_alloc sz :
    ‚ä¢ |==>
      ‚àÉ Œ≥_models,
      models_auth' Œ≥_models sz (replicate sz []) ‚àó
      [‚àó list] i ‚àà seq 0 sz,
        models_at' Œ≥_models i [].
  Proof.
    iMod ghost_list_alloc as "(%Œ≥_models & $ & Hats)".
    iSplitR.
    - iPureIntro. apply length_replicate.
    - iApply (big_sepL_replicate_1 with "Hats").
  Qed.
  #[local] Lemma models_auth_length Œ≥ vss :
    models_auth Œ≥ vss ‚ä¢
    ‚åúlength vss = Œ≥.(metadata_size)‚åù.
  Proof.
    iSteps.
  Qed.
  #[local] Lemma models_lookup Œ≥ vss i vs :
    models_auth Œ≥ vss -‚àó
    models_at Œ≥ i vs -‚àó
    ‚åúvss !! i = Some vs‚åù.
  Proof.
    iIntros "(:models_auth) Hat".
    iApply (ghost_list_lookup with "Hauth Hat").
  Qed.
  #[local] Lemma models_update {Œ≥ vss i vs} vs' :
    models_auth Œ≥ vss -‚àó
    models_at Œ≥ i vs ==‚àó
      models_auth Œ≥ (<[i := vs']> vss) ‚àó
      models_at Œ≥ i vs'.
  Proof.
    iIntros "(:models_auth) Hat".
    iMod (ghost_list_update_at with "Hauth Hat") as "($ & $)".
    iPureIntro. simpl_length.
  Qed.

  Opaque models_auth'.

  #[local] Lemma owner_alloc sz :
    ‚ä¢ |==>
      ‚àÉ Œ≥_owners,
      ( [‚àó list] i ‚àà seq 0 sz,
        owner‚ÇÅ' Œ≥_owners i Nonblocked
      ) ‚àó
      ( [‚àó list] i ‚àà seq 0 sz,
        owner‚ÇÇ' Œ≥_owners i Nonblocked
      ).
  Proof.
    iAssert (
      [‚àó list] _ ‚àà seq 0 sz,
        |==>
        ‚àÉ Œ≥_owner,
        twins_twin1 (twins_G := ws_queues_private_G_owner_G) Œ≥_owner (DfracOwn 1) Nonblocked ‚àó
        twins_twin2 (twins_G := ws_queues_private_G_owner_G) Œ≥_owner Nonblocked
    )%I as "-#H".
    { iApply big_sepL_intro. iIntros "!> % % _".
      iApply twins_alloc'.
    }
    iMod (big_sepL_bupd with "H") as "H".
    iDestruct (big_sepL_exists with "H") as "(%Œ≥_owners & _ & H)".
    iDestruct (big_sepL2_sep with "H") as "(H1 & H2)".
    iDestruct (big_sepL2_retract_r with "H1") as "(_ & H1)".
    iDestruct (big_sepL2_retract_r with "H2") as "(_ & H2)".
    iDestruct (big_sepL_seq_index_2 with "H1") as "H1".
    { simpl_length. }
    iDestruct (big_sepL_seq_index_2 with "H2") as "H2".
    { simpl_length. }
    iSteps.
  Qed.
  #[local] Lemma owner_agree Œ≥ i status1 status2 :
    owner‚ÇÅ Œ≥ i status1 -‚àó
    owner‚ÇÇ Œ≥ i status2 -‚àó
    ‚åústatus1 = status2‚åù.
  Proof.
    iIntros "(:owner‚ÇÅ =1) (:owner‚ÇÇ =2)". simplify.
    iApply (twins_agree_L with "Htwin‚ÇÅ Htwin‚ÇÇ").
  Qed.
  #[local] Lemma owner_update {Œ≥ i status1 status2} status :
    owner‚ÇÅ Œ≥ i status1 -‚àó
    owner‚ÇÇ Œ≥ i status2 ==‚àó
      owner‚ÇÅ Œ≥ i status ‚àó
      owner‚ÇÇ Œ≥ i status.
  Proof.
    iIntros "(:owner‚ÇÅ =1) (:owner‚ÇÇ =2)". simplify.
    iMod (twins_update with "Htwin‚ÇÅ Htwin‚ÇÇ") as "(Htwin‚ÇÅ & Htwin‚ÇÇ)".
    iSteps.
  Qed.

  Opaque owner‚ÇÅ'.
  Opaque owner‚ÇÇ'.

  #[local] Lemma channels_alloc sz :
    ‚ä¢ |==>
      ‚àÉ Œ≥_channels,
      ( [‚àó list] i ‚àà seq 0 sz,
        channels_waiting' Œ≥_channels i
      ) ‚àó
      ( [‚àó list] i ‚àà seq 0 sz,
        channels_sender' Œ≥_channels i inhabitant None ‚àó
        channels_receiver' Œ≥_channels i inhabitant None
      ).
  Proof.
    iAssert (
      [‚àó list] _ ‚àà seq 0 sz,
        |==>
        ‚àÉ Œ≥_channel,
        ( ‚àÉ gen,
          ghost_var (ghost_var_G := ws_queues_private_G_channel_generation_G) Œ≥_channel.2 (DfracOwn (1/2)) gen ‚àó
          oneshot_pending gen (DfracOwn 1) ()
        ) ‚àó
        ( ‚àÉ gen,
          ghost_pred Œ≥_channel.1 (DfracOwn (3/4)) inhabitant ‚àó
          ghost_pred Œ≥_channel.1 (DfracOwn (1/4)) inhabitant ‚àó
          ghost_var Œ≥_channel.2 (DfracOwn (1/2)) gen
        )
    )%I as "-#H".
    { iApply big_sepL_intro. iIntros "!> % % _".
      iMod (ghost_pred_alloc inhabitant) as "(%Œ≥_pred & Hpred)".
      iEval (rewrite -Qp.three_quarter_quarter) in "Hpred". iDestruct "Hpred" as "(Hpred_1 & Hpred_2)".
      iMod (oneshot_alloc ()) as "(%gen & Hpending)".
      iMod (ghost_var_alloc (ghost_var_G := ws_queues_private_G_channel_generation_G) gen) as "(%Œ≥_state & Hgeneration_1 & Hgeneration_2)".
      iExists (Œ≥_pred, Œ≥_state). iSteps.
    }
    iMod (big_sepL_bupd with "H") as "H".
    iDestruct (big_sepL_exists with "H") as "(%Œ≥_channels & _ & H)".
    iDestruct (big_sepL2_sep with "H") as "(H1 & H2)".
    iDestruct (big_sepL2_retract_r with "H1") as "(_ & H1)".
    iDestruct (big_sepL2_retract_r with "H2") as "(_ & H2)".
    iDestruct (big_sepL_seq_index_2 with "H1") as "H1".
    { simpl_length. }
    iDestruct (big_sepL_seq_index_2 with "H2") as "H2".
    { simpl_length. }
    iExists Œ≥_channels. iSplitL "H1".
    1: iApply (big_sepL_impl with "H1").
    2: iApply (big_sepL_impl with "H2").
    all: iSteps.
  Qed.
  #[local] Lemma channels_sender_exclusive Œ≥ i Œ®1 state1 Œ®2 state2 :
    channels_sender Œ≥ i Œ®1 state1 -‚àó
    channels_sender Œ≥ i Œ®2 state2 -‚àó
    False.
  Proof.
    iIntros "(:channels_sender =1) (:channels_sender =2)". simplify_eq.
    iDestruct (ghost_pred_dfrac_ne with "Hpred_1 Hpred_2") as %?; naive_solver.
  Qed.
  #[local] Lemma channels_waiting_receiver Œ≥ i Œ® o :
    ‚ñ∑ channels_waiting Œ≥ i -‚àó
    channels_receiver Œ≥ i Œ® (Some o) -‚àó
    ‚óá False.
  Proof.
    iIntros ">(:channels_waiting =1) (:channels_receiver =2 done=)". simplify_eq.
    iDestruct (ghost_var_agree_L with "Hgeneration_1 Hgeneration_2") as %<-.
    iApply (oneshot_pending_shot with "Hpending_1 Hshot_2").
  Qed.
  #[local] Lemma channels_sender_receiver_agree Œ≥ i Œ®1 o1 Œ®2 o2 E :
    ‚ñ∑ channels_sender Œ≥ i Œ®1 (Some o1) -‚àó
    channels_receiver Œ≥ i Œ®2 (Some o2) ={E}=‚àó
      ‚ñ∑^2 (Œ®1 o1 ‚â° Œ®2 o1) ‚àó
      ‚åúo1 = o2‚åù ‚àó
      ‚ñ∑ channels_sender Œ≥ i Œ®1 (Some o1) ‚àó
      channels_receiver Œ≥ i Œ®2 (Some o1).
  Proof.
    iIntros "(:channels_sender =1 > done=) (:channels_receiver =2 done=)". simplify_eq.
    iDestruct "Hgeneration_1" as ">Hgeneration_1".
    iDestruct "Hshot_1" as ">Hshot_1".
    iDestruct (ghost_pred_agree o1 with "Hpred_1 [$Hpred_2]") as "#Heq".
    iDestruct (ghost_var_agree_L with "Hgeneration_1 Hgeneration_2") as %<-.
    iDestruct (oneshot_shot_agree with "Hshot_1 Hshot_2") as %<-.
    iFrame "#‚àó". iSteps.
  Qed.
  #[local] Lemma channels_prepare {Œ≥ i Œ®1 Œ®2} Œ® :
    channels_sender Œ≥ i Œ®1 None -‚àó
    channels_receiver Œ≥ i Œ®2 None ==‚àó
      channels_sender Œ≥ i Œ® None ‚àó
      channels_receiver Œ≥ i Œ® None.
  Proof.
    iIntros "(:channels_sender =1) (:channels_receiver =2)". simplify_eq.
    iDestruct (ghost_pred_combine inhabitant with "Hpred_1 Hpred_2") as "(_ & Hpred)". rewrite dfrac_op_own Qp.three_quarter_quarter.
    iMod (ghost_pred_update Œ® with "Hpred") as "Hpred".
    iEval (rewrite -Qp.three_quarter_quarter) in "Hpred". iDestruct "Hpred" as "(Hpred_1 & Hpred_2)".
    iSteps.
  Qed.
  #[local] Lemma channels_send {Œ≥ i Œ®} o :
    channels_waiting Œ≥ i -‚àó
    channels_sender Œ≥ i Œ® None ==‚àó
    channels_sender Œ≥ i Œ® (Some o).
  Proof.
    iIntros "(:channels_waiting =1) (:channels_sender =2)". simplify_eq.
    iMod (oneshot_update_shot o with "Hpending_1") as "#Hshot".
    iSteps.
  Qed.
  #[local] Lemma channels_receive Œ≥ i Œ®1 Œ®2 o :
    ‚ñ∑ channels_sender Œ≥ i Œ®1 (Some o) -‚àó
    channels_receiver Œ≥ i Œ®2 None -‚àó
    ‚óá (
      ‚ñ∑ channels_sender Œ≥ i Œ®1 (Some o) ‚àó
      channels_receiver Œ≥ i Œ®2 (Some o)
    ).
  Proof.
    iIntros "(:channels_sender =1 > done=) (:channels_receiver =2)". simplify_eq.
    iDestruct "Hgeneration_1" as ">Hgeneration_1".
    iDestruct "Hshot_1" as ">Hshot_1".
    iDestruct (ghost_var_agree_L with "Hgeneration_1 Hgeneration_2") as %<-.
    iModIntro. iFrameSteps.
  Qed.
  #[local] Lemma channels_reset Œ≥ i Œ®1 o1 Œ®2 o2 E :
    ‚ñ∑ channels_sender Œ≥ i Œ®1 (Some o1) -‚àó
    channels_receiver Œ≥ i Œ®2 (Some o2) ={E}=‚àó
      channels_waiting Œ≥ i ‚àó
      ‚ñ∑ channels_sender Œ≥ i Œ®1 None ‚àó
      channels_receiver Œ≥ i Œ®2 None.
  Proof.
    iIntros "(:channels_sender =1 > done=) (:channels_receiver =2)". simplify_eq.
    iDestruct "Hgeneration_1" as ">Hgeneration_1".
    iMod (oneshot_alloc ()) as "(%gen & Hpending)".
    iDestruct (ghost_var_combine with "Hgeneration_1 Hgeneration_2") as "(_ & Hgeneration)". rewrite dfrac_op_own Qp.half_half.
    iMod (ghost_var_update (ghost_var_G := ws_queues_private_G_channel_generation_G) gen with "Hgeneration") as "Hgeneration".
    iDestruct "Hgeneration" as "(Hgeneration_1 & Hgeneration_2)".
    iSteps.
  Qed.

  Opaque channels_waiting'.
  Opaque channels_sender'.
  Opaque channels_receiver'.

  #[local] Lemma request_model_update {Œ≥ i request} request' :
    (request' = RequestBlocked ‚à® request' = RequestNone) ‚Üí
    ‚ñ∑ request_model Œ≥ i request -‚àó
    owner‚ÇÅ Œ≥ i Nonblocked -‚àó
    ‚óá (
      ‚ñ∑ request_model Œ≥ i request' ‚àó
      owner‚ÇÅ Œ≥ i Nonblocked ‚àó
      if request is RequestSome j then
        ‚ñ∑ request_model_nonblocked' Œ≥ i j
      else
        True
    ).
  Proof.
    iIntros "%Hrequest' Hrequest Howner‚ÇÅ".
    destruct request as [| | j].
    1,2: iFrame; naive_solver.
    iDestruct "Hrequest" as "(:request_model >)".
    - iDestruct (owner_agree with "Howner‚ÇÅ Howner‚ÇÇ") as %[=].
    - iFrame. naive_solver.
  Qed.
  #[local] Lemma request_model_respond Œ≥ i request :
    ‚ñ∑ request_model Œ≥ i request -‚àó
    owner‚ÇÅ Œ≥ i Nonblocked ==‚àó
    ‚óá (
      ‚ñ∑ request_model Œ≥ i request ‚àó
      if request is RequestSome j then
        owner‚ÇÅ Œ≥ i Blocked ‚àó
        ‚ñ∑ request_model_nonblocked' Œ≥ i j
      else
        owner‚ÇÅ Œ≥ i Nonblocked
    ).
  Proof.
    iIntros "Hrequest Howner‚ÇÅ".
    destruct request as [| | j].
    1,2: iFrame; naive_solver.
    iDestruct "Hrequest" as "(:request_model >)".
    - iDestruct (owner_agree with "Howner‚ÇÅ Howner‚ÇÇ") as %[=].
    - iMod (owner_update Blocked with "Howner‚ÇÅ Howner‚ÇÇ") as "(Howner‚ÇÅ & Howner‚ÇÇ)".
      iFrameSteps.
  Qed.
  #[local] Lemma request_model_unblock Œ≥ i request :
    ‚ñ∑ request_model Œ≥ i request -‚àó
    owner‚ÇÅ Œ≥ i Blocked ==‚àó
    ‚óá (
      ‚ñ∑ request_model Œ≥ i RequestNone ‚àó
      owner‚ÇÅ Œ≥ i Nonblocked
    ).
  Proof.
    iIntros "Hrequest Howner‚ÇÅ".
    destruct request as [| | j].
    1,2: iDestruct "Hrequest" as ">Howner‚ÇÇ".
    1,2: iDestruct (owner_agree with "Howner‚ÇÅ Howner‚ÇÇ") as %[=].
    iDestruct "Hrequest" as "(:request_model >)".
    - iMod (owner_update with "Howner‚ÇÅ Howner‚ÇÇ") as "(Howner‚ÇÅ & Howner‚ÇÇ)".
      iFrameSteps.
    - iDestruct (owner_agree with "Howner‚ÇÅ Howner‚ÇÇ") as %[=].
  Qed.

  #[local] Lemma response_model_sender Œ≥ i response Œ® state :
    ‚ñ∑ response_model Œ≥ i response -‚àó
    channels_sender Œ≥ i Œ® state -‚àó
    ‚óá (
      ‚åúresponse = ResponseWaiting‚åù ‚àó
      channels_waiting Œ≥ i ‚àó
      channels_sender Œ≥ i Œ® state
    ).
  Proof.
    iIntros "Hresponse Hchannels_sender".
    destruct response.
    1: iDestruct "Hresponse" as ">Hresponse".
    1: iModIntro; iSteps.
    all: iDestruct "Hresponse" as "(:response_model =1)".
    all: iDestruct (channels_sender_exclusive with "Hchannels_sender Hchannels_sender_1") as ">%".
    all: done.
  Qed.
  #[local] Lemma response_model_receiver Œ≥ i response Œ® o E :
    ‚ñ∑ response_model Œ≥ i response -‚àó
    channels_receiver Œ≥ i Œ® (Some o) ={E}=‚àó
      ‚àÉ Œ®_,
      ‚ñ∑^2 (Œ®_ o ‚â° Œ® o) ‚àó
      ‚åúresponse = o‚åù ‚àó
      ‚ñ∑ channels_sender Œ≥ i Œ®_ (Some o) ‚àó
      channels_receiver Œ≥ i Œ® (Some o) ‚àó
      ‚ñ∑ Œ®_ o.
  Proof.
    iIntros "Hresponse Hchannels_receiver".
    destruct response.
    1: iMod (channels_waiting_receiver with "Hresponse Hchannels_receiver") as %[].
    all: iDestruct "Hresponse" as "(:response_model =1)".
    all: iMod (channels_sender_receiver_agree with "Hchannels_sender_1 Hchannels_receiver") as "(Heq & <- & $ & $)".
    all: iFrame "#‚àó"; iSteps.
  Qed.

  Lemma ws_queues_private_inv_agree t Œπ1 sz1 Œπ2 sz2 :
    ws_queues_private_inv t Œπ1 sz1 -‚àó
    ws_queues_private_inv t Œπ2 sz2 -‚àó
    ‚åúsz1 = sz2‚åù.
  Proof.
    iIntros "(:inv =1) (:inv =2)". simplify.
    iDestruct (pointsto_agree with "Hl1_size Hl2_size") as %?. naive_solver.
  Qed.

  Lemma ws_queues_private_owner_exclusive t i status1 ws1 status2 ws2 :
    ws_queues_private_owner t i status1 ws1 -‚àó
    ws_queues_private_owner t i status2 ws2 -‚àó
    False.
  Proof.
    iIntros "(:owner =1) (:owner =2)". simplify.
    iDestruct (meta_agree with "Hmeta_1 Hmeta_2") as %<-. simplify.
    iApply (queue_3_model_exclusive with "Hqueue_model_1 Hqueue_model_2").
  Qed.

  Lemma ws_queues_private_inv_model t Œπ sz vss :
    ws_queues_private_inv t Œπ sz -‚àó
    ws_queues_private_model t vss -‚àó
    ‚åúlength vss = sz‚åù.
  Proof.
    iIntros "(:inv) (:model)". injection Heq as <-.
    iDestruct (meta_agree with "Hmeta Hmeta_") as %<-.
    iApply (models_auth_length with "Hmodels_auth").
  Qed.
  Lemma ws_queues_private_inv_owner t Œπ sz i status ws :
    ws_queues_private_inv t Œπ sz -‚àó
    ws_queues_private_owner t i status ws -‚àó
    ‚åúi < sz‚åù.
  Proof.
    iIntros "(:inv) (:owner)". injection Heq as <-.
    iDestruct (meta_agree with "Hmeta Hmeta_") as %<-.
    apply lookup_lt_Some in Hqueues_lookup.
    iSteps.
  Qed.

  Lemma ws_queues_private_model_owner t vss i status ws :
    ws_queues_private_model t vss -‚àó
    ws_queues_private_owner t i status ws -‚àó
      ‚àÉ vs,
      ‚åúvss !! i = Some vs‚åù ‚àó
      ‚åúvs `suffix_of` ws‚åù.
  Proof.
    iIntros "(:model =1) (:owner =2)". simplify.
    iDestruct (meta_agree with "Hmeta_1 Hmeta_2") as %<-. simplify.
    iDestruct (models_lookup with "Hmodels_auth_1 Hmodels_at_2") as %Hlookup.
    iSteps.
  Qed.

  Lemma ws_queues_private_create_spec Œπ sz :
    (0 ‚â§ sz)%Z ‚Üí
    {{{
      True
    }}}
      ws_queues_private_create #sz
    {{{ t,
      RET t;
      ws_queues_private_inv t Œπ ‚Çäsz ‚àó
      ws_queues_private_model t (replicate ‚Çäsz []) ‚àó
      [‚àó list] i ‚àà seq 0 ‚Çäsz,
        ws_queues_private_owner t i Nonblocked []
    }}}.
  Proof.
    iIntros "%Hsz %Œ¶ _ HŒ¶".

    wp_rec.
    wp_apply (array_unsafe_make_spec with "[//]") as (responses_array) "Hresponses_model"; first done.
    iDestruct (array_model_to_inv with "Hresponses_model") as "#Hresponses_inv".
    wp_apply (array_make_spec with "[//]") as (requests_array) "(_ & Hrequests_model)".
    iDestruct (array_model_to_inv with "Hrequests_model") as "#Hrequests_inv".
    wp_apply (array_unsafe_make_spec with "[//]") as (statuses_array) "Hstatuses_model"; first done.
    iDestruct (array_model_to_inv with "Hstatuses_model") as "#Hstatuses_inv".
    wp_apply (array_unsafe_init_spec_disentangled (Œª _ queue, queue_3_model queue [])) as (queues_array queues) "(%Hqueues_length & Hqueues_model & Hqueues)"; first done.
    { iIntros "!> %i %Hi".
      wp_apply (queue_3_create_spec with "[//]").
      iSteps.
    }
    iDestruct (array_model_to_inv with "Hqueues_model") as "#Hqueues_inv".
    iMod (array_model_persist with "Hqueues_model") as "#Hqueues_model".
    wp_block l as "Hmeta" "(Hl_size & Hl_queues & Hl_statuses & Hl_requests & Hl_responses & _)".
    iMod (pointsto_persist with "Hl_size") as "#Hl_size".
    iMod (pointsto_persist with "Hl_queues") as "#Hl_queues".
    iMod (pointsto_persist with "Hl_statuses") as "#Hl_statuses".
    iMod (pointsto_persist with "Hl_requests") as "#Hl_requests".
    iMod (pointsto_persist with "Hl_responses") as "#Hl_responses".

    iMod models_alloc as "(%Œ≥_models & Hmodels_auth & Hmodels_ats)".
    iMod owner_alloc as "(%Œ≥_owners & Howners‚ÇÅ & Howners‚ÇÇ)".
    iMod channels_alloc as "(%Œ≥_channels & Hchannels_1 & Hchannels_2)".

    pose Œ≥ := {|
      metadata_queues_array := queues_array ;
      metadata_queues := queues ;
      metadata_statuses_array := statuses_array ;
      metadata_requests_array := requests_array ;
      metadata_responses_array := responses_array ;
      metadata_size := ‚Çäsz ;
      metadata_inv := Œπ ;
      metadata_models := Œ≥_models ;
      metadata_owners := Œ≥_owners ;
      metadata_channels := Œ≥_channels ;
    |}.
    iMod (meta_set Œ≥ with "Hmeta") as "#Hmeta"; first done.

    iApply "HŒ¶".
    iSplitR "Hmodels_auth Hqueues Hmodels_ats Howners‚ÇÅ Hchannels_2".

    - rewrite Hqueues_length. simpl_length.
      iEval (rewrite -(fmap_replicate status_to_val _ Nonblocked)) in "Hstatuses_model".
      iEval (rewrite -(fmap_replicate request_to_val _ RequestNone)) in "Hrequests_model".
      iEval (rewrite -(fmap_replicate response_to_val _ ResponseWaiting)) in "Hresponses_model".
      iExists l, Œ≥. rewrite Z2Nat.id //. iStep 14.
      iApply inv_alloc.
      iSteps. iSplitL "Howners‚ÇÇ" => /=.
      + iApply big_sepL_replicate_2.
        iApply (big_sepL_impl with "Howners‚ÇÇ").
        iSteps.
      + rewrite big_op.big_sepL_replicate.
        iApply (big_sepL_impl with "Hchannels_1").
        iSteps.

    - iSteps.
      iDestruct (big_sepL_sep_2 with "Hmodels_ats Howners‚ÇÅ") as "H".
      iDestruct (big_sepL_sep_2 with "H Hchannels_2") as "H".
      iDestruct (big_sepL_to_seq0 with "Hqueues") as "Hqueues". rewrite Hqueues_length.
      iDestruct (big_sepL_sep_2 with "Hqueues H") as "H".
      iApply (big_sepL_impl with "H").
      iSteps.
  Qed.

  Lemma ws_queues_private_size_spec t Œπ sz :
    {{{
      ws_queues_private_inv t Œπ sz
    }}}
      ws_queues_private_size t
    {{{
      RET #sz;
      True
    }}}.
  Proof.
    iIntros "%Œ¶ (:inv) HŒ¶".

    wp_rec. wp_load.
    iSteps.
  Qed.

  Lemma ws_queues_private_block_spec t Œπ sz i i_ ws :
    i = ‚Å∫i_ ‚Üí
    {{{
      ws_queues_private_inv t Œπ sz ‚àó
      ws_queues_private_owner t i_ Nonblocked ws
    }}}
      ws_queues_private_block t #i
    {{{
      RET ();
      ws_queues_private_owner t i_ Blocked ws
    }}}.
  Proof.
    iIntros (->) "%Œ¶ ((:inv) & (:owner)) HŒ¶". injection Heq as <-.
    iDestruct (meta_agree with "Hmeta Hmeta_") as %<-. iClear "Hmeta_".
    opose proof* lookup_lt_Some; first done.

    wp_rec.
    iApply (wp_frame_wand with "[- Howner‚ÇÅ]"); first iAccu.
    wp_load.

    awp_apply (array_unsafe_set_spec_atomic_inv with "Hstatuses_inv") without "Howner‚ÇÅ"; first lia.
    iInv "Hinv" as "(:inv_inner =1)".
    iAaccIntro with "Hstatuses_model"; first iSteps.
    rewrite Nat2Z.id -(list_fmap_insert _ _ _ Blocked).
    iIntros "%ùë†ùë°ùëéùë°ùë¢ùë† (_ & Hstatuses_model) !>".
    iSplitL. { iFrameSteps. }
    iIntros "_ Howner‚ÇÅ".

    wp_load.

    awp_apply (array_unsafe_xchg_spec_atomic_inv with "Hrequests_inv"); first lia.
    iInv "Hinv" as "(:inv_inner =2)".
    iAaccIntro with "Hrequests_model"; first iSteps.
    rewrite Nat2Z.id -(list_fmap_insert _ _ _ RequestBlocked).
    iIntros "%ùëüùëíùëûùë¢ùëíùë†ùë° (%Hrequests2_lookup & Hrequests_model)".
    apply list_lookup_fmap_Some in Hrequests2_lookup as (request & Hrequests2_lookup & ->).
    iDestruct (big_sepL_insert_acc with "Hrequests") as "(Hrequest & Hrequests)"; first done.
    iMod (request_model_update RequestBlocked with "Hrequest Howner‚ÇÅ") as "(Hrequest & Howner‚ÇÅ & H)"; first auto.
    iDestruct ("Hrequests" $! RequestBlocked with "Hrequest") as "Hrequests".
    iSplitR "Howner‚ÇÅ H". { iFrameSteps. }
    iIntros "!> _".

    destruct request as [| | j]; [iSteps.. |].
    iDestruct "H" as "(:request_model_nonblocked' >)".

    wp_load.

    iApply fupd_wp.
    iMod "HŒß" as "(%vss & Hmodels_auth & _ & HŒß)".
    iMod ("HŒß" $! None with "Hmodels_auth") as "HŒß".
    iModIntro.

    awp_apply (array_unsafe_set_spec_atomic_inv with "Hresponses_inv") without "Howner‚ÇÅ"; first lia.
    iInv "Hinv" as "(:inv_inner =3)".
    iAaccIntro with "Hresponses_model"; first iSteps.
    rewrite Nat2Z.id -(list_fmap_insert _ _ _ ResponseNone).
    iIntros "%ùëüùëíùë†ùëùùëúùëõùë†ùëí (%Hresponses3_lookup & Hresponses_model)".
    apply list_lookup_fmap_inv in Hresponses3_lookup as (reponse & -> & Hresponses3_lookup).
    iDestruct (big_sepL_insert_acc with "Hresponses") as "(Hresponse & Hresponses)"; first done.
    iMod (response_model_sender with "Hresponse Hchannels_sender") as "(-> & Hchannels_waiting & Hchannels_sender)".
    iMod (channels_send with "Hchannels_waiting Hchannels_sender") as "Hchannels_sender".
    iSplitL. { iFrameSteps. }
    iSteps.
  Qed.

  Lemma ws_queues_private_unblock_spec t Œπ sz i i_ ws :
    i = ‚Å∫i_ ‚Üí
    {{{
      ws_queues_private_inv t Œπ sz ‚àó
      ws_queues_private_owner t i_ Blocked ws
    }}}
      ws_queues_private_unblock t #i
    {{{
      RET ();
      ws_queues_private_owner t i_ Nonblocked ws
    }}}.
  Proof.
    iIntros (->) "%Œ¶ ((:inv) & (:owner)) HŒ¶". injection Heq as <-.
    iDestruct (meta_agree with "Hmeta Hmeta_") as %<-. iClear "Hmeta_".
    opose proof* lookup_lt_Some; first done.

    wp_rec.
    iApply (wp_frame_wand with "[- Howner‚ÇÅ]"); first iAccu.
    wp_load.

    awp_apply (array_unsafe_set_spec_atomic_inv with "Hrequests_inv"); first lia.
    iInv "Hinv" as "(:inv_inner =1)".
    iAaccIntro with "Hrequests_model"; first iSteps.
    rewrite Nat2Z.id -(list_fmap_insert _ _ _ RequestNone).
    iIntros "%ùëüùëíùëûùë¢ùëíùë†ùë° (%Hrequests1_lookup & Hrequests_model)".
    apply list_lookup_fmap_inv in Hrequests1_lookup as (request & -> & Hrequests1_lookup).
    iDestruct (big_sepL_insert_acc with "Hrequests") as "(Hrequest & Hrequests)"; first done.
    iMod (request_model_update RequestNone with "Hrequest Howner‚ÇÅ") as "(Hrequest & Howner‚ÇÅ & H)"; first auto.
    iDestruct ("Hrequests" $! RequestNone with "Hrequest") as "Hrequests".
    iSplitR "Howner‚ÇÅ". { iFrameSteps. }
    iIntros "!> _".

    wp_load.

    awp_apply (array_unsafe_set_spec_atomic_inv with "Hstatuses_inv") without "Howner‚ÇÅ"; first lia.
    iInv "Hinv" as "(:inv_inner =2)".
    iAaccIntro with "Hstatuses_model"; first iSteps.
    rewrite Nat2Z.id -(list_fmap_insert _ _ _ Nonblocked).
    iIntros "%ùë†ùë°ùëéùë°ùë¢ùë† (_ & Hstatuses_model) !>".
    iSplitL. { iFrameSteps. }
    iSteps.
  Qed.

  #[local] Lemma ws_queues_private_respond_spec {t Œπ sz i i_} ws :
    i = ‚Å∫i_ ‚Üí
    {{{
      ws_queues_private_inv t Œπ sz ‚àó
      ws_queues_private_owner t i_ Nonblocked ws
    }}}
      ws_queues_private_respond t #i
    {{{
      RET ();
      ws_queues_private_owner t i_ Nonblocked ws
    }}}.
  Proof.
    iIntros (->) "%Œ¶ ((:inv) & (:owner)) HŒ¶". injection Heq as <-.
    iDestruct (meta_agree with "Hmeta Hmeta_") as %<-. iClear "Hmeta_".
    opose proof* lookup_lt_Some; first done.

    wp_rec.
    iApply (wp_frame_wand with "[- Hqueue_model Hmodels_at Howner‚ÇÅ]"); first iAccu.
    wp_load.

    awp_apply (array_unsafe_get_spec_atomic_inv with "Hrequests_inv") without "Hqueue_model Hmodels_at"; first lia.
    iInv "Hinv" as "(:inv_inner =1)".
    iAaccIntro with "Hrequests_model"; first iSteps.
    rewrite Nat2Z.id.
    iIntros "%ùëüùëíùëûùë¢ùëíùë†ùë° (%Hrequests1_lookup & Hrequests_model)".
    apply list_lookup_fmap_Some in Hrequests1_lookup as (request & Hrequests1_lookup & ->).
    iDestruct (big_sepL_lookup_acc with "Hrequests") as "(Hrequest & Hrequests)"; first done.
    iMod (request_model_respond with "Hrequest Howner‚ÇÅ") as ">(Hrequest & H)".
    iDestruct ("Hrequests" with "Hrequest") as "Hrequests".
    iSplitR "H". { iFrameSteps. }
    iIntros "!> _ (Hqueue_model & Hmodels_at)".

    destruct request as [| | j]; [iSteps.. |].
    iDestruct "H" as "(Howner‚ÇÅ & (:request_model_nonblocked' >))".

    wp_load.
    wp_apply (array_unsafe_get_spec with "Hqueues_model") as "_"; [lia | done | lia |].
    wp_apply (queue_3_pop_front_spec with "Hqueue_model") as "Hqueue_model".

    wp_bind (Match _ _ _ _)%E.
    wp_apply (wp_wand (Œª res,
      ‚åúres = response_to_val $ head vs‚åù
    )%I) as "%res ->".
    { destruct vs; iSteps. }

    wp_load.

    awp_apply (array_unsafe_set_spec_atomic_inv with "Hresponses_inv") without "Hqueue_model Howner‚ÇÅ"; first lia.
    iInv "Hinv" as "(:inv_inner =2)".
    iAaccIntro with "Hresponses_model"; first iSteps.
    rewrite Nat2Z.id -list_fmap_insert.
    iIntros "%ùëüùëíùë†ùëùùëúùëõùë†ùëí (%Hresponses2_lookup & Hresponses_model)".
    apply list_lookup_fmap_Some in Hresponses2_lookup as (response & Hresponses2_lookup & ->).
    iDestruct (big_sepL_insert_acc with "Hresponses") as "(Hresponse & Hresponses)"; first done.
    iMod (response_model_sender with "Hresponse Hchannels_sender") as "(-> & Hchannels_waiting & Hchannels_sender)".
    iMod (channels_send (head vs) with "Hchannels_waiting Hchannels_sender") as "Hchannels_sender".

    iAssert (
      |={_}=>
      models_at Œ≥ i_ (tail vs) ‚àó
      response_model Œ≥ j (head vs)
    )%I with "[Hmodels_at Hchannels_sender HŒß]" as ">(Hmodels_at & Hresponse)".
    { iMod "HŒß" as "(%vss & Hmodels_auth & _ & HŒß)".
      iDestruct (models_lookup with "Hmodels_auth Hmodels_at") as %Hvss_lookup.
      destruct vs as [| v vs]; first iSteps.
      iMod (models_update vs with "Hmodels_auth Hmodels_at") as "(Hmodels_auth & Hmodels_at)".
      iMod ("HŒß" $! (Some v) with "[$Hmodels_auth //]") as "HŒß".
      iSteps.
    }

    iSplitR "Hmodels_at". { iFrameSteps. }
    iIntros "!> _ (Hqueue_model & Howner‚ÇÅ)".

    wp_load.

    awp_apply (array_unsafe_set_spec_atomic_inv with "Hrequests_inv") without "Hqueue_model Hmodels_at"; first lia.
    iInv "Hinv" as "(:inv_inner =3)".
    iAaccIntro with "Hrequests_model"; first iSteps.
    rewrite Nat2Z.id -(list_fmap_insert _ _ _ RequestNone).
    iIntros "%ùëüùëíùëûùë¢ùëíùë†ùë° (%Hrequests3_lookup & Hrequests_model)".
    apply list_lookup_fmap_inv in Hrequests3_lookup as (request & -> & Hrequests3_lookup).
    iDestruct (big_sepL_insert_acc with "Hrequests") as "(Hrequest & Hrequests)"; first done.
    iMod (request_model_unblock with "Hrequest Howner‚ÇÅ") as ">(Hrequest & Howner‚ÇÅ)".
    iDestruct ("Hrequests" $! RequestNone with "Hrequest") as "Hrequests".
    iSplitR "Howner‚ÇÅ". { iFrameSteps. }
    iIntros "!> _".

    iSteps. iPureIntro. apply suffix_tail. done.
  Qed.

  Lemma ws_queues_private_push_spec t Œπ sz i i_ ws v :
    i = ‚Å∫i_ ‚Üí
    <<<
      ws_queues_private_inv t Œπ sz ‚àó
      ws_queues_private_owner t i_ Nonblocked ws
    | ‚àÄ‚àÄ vss,
      ws_queues_private_model t vss
    >>>
      ws_queues_private_push t #i v @ ‚ÜëŒπ
    <<<
      ‚àÉ‚àÉ vs,
      ‚åúvss !! i_ = Some vs‚åù ‚àó
      ‚åúvs `suffix_of` ws‚åù ‚àó
      ws_queues_private_model t (<[i_ := vs ++ [v]]> vss)
    | RET ();
      ws_queues_private_owner t i_ Nonblocked (vs ++ [v])
    >>>.
  Proof.
    iIntros (->) "%Œ¶ ((:inv) & (:owner)) HŒ¶". injection Heq as <-.
    iDestruct (meta_agree with "Hmeta Hmeta_") as %<-. iClear "Hmeta_".

    wp_rec. wp_load.
    wp_apply (array_unsafe_get_spec with "Hqueues_model") as "_"; [lia | done | lia |].
    wp_apply (queue_3_push_spec with "Hqueue_model") as "Hqueue_model".

    iApply fupd_wp.
    iMod "HŒ¶" as "(%vss & (:model) & _ & HŒ¶)". injection Heq as <-.
    iDestruct (meta_agree with "Hmeta Hmeta_") as %<-. iClear "Hmeta_".
    iDestruct (models_lookup with "Hmodels_auth Hmodels_at") as %Hvss_lookup.
    iMod (models_update (vs ++ [v]) with "Hmodels_auth Hmodels_at") as "(Hmodels_auth & Hmodels_at)".
    iMod ("HŒ¶" with "[Hmodels_auth]") as "HŒ¶"; first iSteps.
    iModIntro.

    wp_smart_apply (ws_queues_private_respond_spec with "[- HŒ¶] HŒ¶"); [done | iFrameSteps].
  Qed.

  Lemma ws_queues_private_pop_spec t Œπ sz i i_ ws :
    i = ‚Å∫i_ ‚Üí
    <<<
      ws_queues_private_inv t Œπ sz ‚àó
      ws_queues_private_owner t i_ Nonblocked ws
    | ‚àÄ‚àÄ vss,
      ws_queues_private_model t vss
    >>>
      ws_queues_private_pop t #i @ ‚ÜëŒπ
    <<<
      ‚àÉ‚àÉ o ws',
      match o with
      | None =>
          ‚åúvss !! i_ = Some []‚åù ‚àó
          ‚åúws' = []‚åù ‚àó
          ws_queues_private_model t vss
      | Some v =>
          ‚àÉ vs,
          ‚åúvss !! i_ = Some (vs ++ [v])‚åù ‚àó
          ‚åúvs ++ [v] `suffix_of` ws‚åù ‚àó
          ‚åúws' = vs‚åù ‚àó
          ws_queues_private_model t (<[i_ := vs]> vss)
      end
    | RET o;
      ws_queues_private_owner t i_ Nonblocked ws'
    >>>.
  Proof.
    iIntros (->) "%Œ¶ ((:inv) & (:owner)) HŒ¶". injection Heq as <-.
    iDestruct (meta_agree with "Hmeta Hmeta_") as %<-. iClear "Hmeta_".

    wp_rec. wp_load.
    wp_apply (array_unsafe_get_spec with "Hqueues_model") as "_"; [lia | done | lia |].
    wp_apply (queue_3_pop_back_spec with "Hqueue_model") as (o) "Hqueue_model".

    iApply fupd_wp.
    iMod "HŒ¶" as "(%vss & (:model) & _ & HŒ¶)". injection Heq as <-.
    iDestruct (meta_agree with "Hmeta Hmeta_") as %<-. iClear "Hmeta_".
    iDestruct (models_lookup with "Hmodels_auth Hmodels_at") as %Hvss_lookup.
    destruct o as [v |].

    - iDestruct "Hqueue_model" as "(%vs' & -> & Hqueue_model)".
      iMod (models_update vs' with "Hmodels_auth Hmodels_at") as "(Hmodels_auth & Hmodels_at)".
      iMod ("HŒ¶" $! (Some v) with "[Hmodels_auth]") as "HŒ¶"; first iSteps.
      iModIntro.

      wp_smart_apply (ws_queues_private_respond_spec with "[- HŒ¶]") as "Howner"; [done | iFrameSteps |].
      wp_pures.
      iApply ("HŒ¶" with "Howner").

    - iDestruct "Hqueue_model" as "(-> & Hqueue_model)".
      iMod ("HŒ¶" $! None with "[Hmodels_auth]") as "HŒ¶"; first iSteps.
      iModIntro.

      wp_smart_apply (ws_queues_private_respond_spec [] with "[- HŒ¶]") as "Howner"; [done | iFrameSteps |].
      wp_pures.
      iApply ("HŒ¶" with "Howner").
  Qed.

  #[local] Lemma ws_queues_private_steal_to_0_spec l Œ≥ i i_ Œ® :
    i = ‚Å∫i_ ‚Üí
    i_ < Œ≥.(metadata_size) ‚Üí
    {{{
      meta l nroot Œ≥ ‚àó
      l.[responses] ‚Ü¶‚ñ° Œ≥.(metadata_responses_array) ‚àó
      array_inv Œ≥.(metadata_responses_array) Œ≥.(metadata_size) ‚àó
      inv Œ≥.(metadata_inv) (inv_inner Œ≥) ‚àó
      channels_receiver Œ≥ i_ Œ® None
    }}}
      ws_queues_private_steal_to_0 #l #i
    {{{ o Œ®_sender Œ®_receiver,
      RET o : val;
      channels_sender Œ≥ i_ Œ®_sender None ‚àó
      channels_receiver Œ≥ i_ Œ®_receiver None ‚àó
      Œ® o
    }}}.
  Proof.
    iIntros (-> Hi) "%Œ¶ (#Hmeta & #Hl_responses & #Hresponses_inv & #Hinv & Hchannels_receiver) HŒ¶".

    iL√∂b as "HL√∂b".

    wp_rec. wp_load.

    awp_apply (array_unsafe_get_spec_atomic_inv with "Hresponses_inv") without "HŒ¶"; first lia.
    iInv "Hinv" as "(:inv_inner =1)".
    iAaccIntro with "Hresponses_model"; first iSteps.
    rewrite Nat2Z.id.
    iIntros "%ùëüùëíùë†ùëùùëúùëõùë†ùëí (%Hresponses1_lookup & Hresponses_model)".
    apply list_lookup_fmap_Some in Hresponses1_lookup as (response & Hresponses1_lookup & ->).
    destruct response as [| | v].

    - iSplitR "Hchannels_receiver". { iFrameSteps. }
      iIntros "!> _ HŒ¶".

      wp_smart_apply domain_yield_spec.
      wp_smart_apply ("HL√∂b" with "Hchannels_receiver HŒ¶").

    - iDestruct (big_sepL_lookup_acc with "Hresponses") as "(Hresponse & Hresponses)"; first done.
      iDestruct "Hresponse" as "(:response_model =1)".
      iMod (channels_receive with "Hchannels_sender_1 Hchannels_receiver") as "(Hchannels_sender & Hchannels_receiver)".
      iSplitR "Hchannels_receiver". { iFrameSteps. }
      iIntros "!> _ HŒ¶".

      wp_load.

      awp_apply (array_unsafe_set_spec_atomic_inv with "Hresponses_inv") without "HŒ¶"; first lia.
      iInv "Hinv" as "(:inv_inner =2)".
      iAaccIntro with "Hresponses_model"; first iSteps.
      rewrite Nat2Z.id -(list_fmap_insert _ _ _ ResponseWaiting).
      iIntros "%ùëüùëíùë†ùëùùëúùëõùë†ùëí (%Hresponses2_lookup & Hresponses_model)".
      apply list_lookup_fmap_Some in Hresponses2_lookup as (response & Hresponses2_lookup & ->).
      iDestruct (big_sepL_insert_acc with "Hresponses") as "(Hresponse & Hresponses)"; first done.
      iMod (response_model_receiver with "Hresponse Hchannels_receiver") as "(%Œ®_ & Heq & -> & Hchannels_sender & Hchannels_receiver & HŒ®)".
      iMod (channels_reset with "Hchannels_sender Hchannels_receiver") as "(Hchannels_waiting & Hchannels_sender & Hchannels_receiver)".
      iDestruct ("Hresponses" $! ResponseWaiting with "[$Hchannels_waiting]") as "Hresponses".
      iSplitR "Hchannels_sender Hchannels_receiver Heq HŒ®". { iFrameSteps. }
      iIntros "!> H¬£ HŒ¶".

      wp_pures.
      iMod (lc_fupd_elim_later with "H¬£ Heq") as "Heq".
      iRewrite "Heq" in "HŒ®".
      iApply ("HŒ¶" $! None).
      iSteps.

    - iDestruct (big_sepL_lookup_acc with "Hresponses") as "(Hresponse & Hresponses)"; first done.
      iDestruct "Hresponse" as "(:response_model =1)".
      iMod (channels_receive with "Hchannels_sender_1 Hchannels_receiver") as "(Hchannels_sender & Hchannels_receiver)".
      iSplitR "Hchannels_receiver". { iFrameSteps. }
      iIntros "!> _ HŒ¶".

      wp_load.

      awp_apply (array_unsafe_set_spec_atomic_inv with "Hresponses_inv") without "HŒ¶"; first lia.
      iInv "Hinv" as "(:inv_inner =2)".
      iAaccIntro with "Hresponses_model"; first iSteps.
      rewrite Nat2Z.id -(list_fmap_insert _ _ _ ResponseWaiting).
      iIntros "%ùëüùëíùë†ùëùùëúùëõùë†ùëí (%Hresponses2_lookup & Hresponses_model)".
      apply list_lookup_fmap_Some in Hresponses2_lookup as (response & Hresponses2_lookup & ->).
      iDestruct (big_sepL_insert_acc with "Hresponses") as "(Hresponse & Hresponses)"; first done.
      iMod (response_model_receiver with "Hresponse Hchannels_receiver") as "(%Œ®_ & Heq & -> & Hchannels_sender & Hchannels_receiver & HŒ®)".
      iMod (channels_reset with "Hchannels_sender Hchannels_receiver") as "(Hchannels_waiting & Hchannels_sender & Hchannels_receiver)".
      iDestruct ("Hresponses" $! ResponseWaiting with "[$Hchannels_waiting]") as "Hresponses".
      iSplitR "Hchannels_sender Hchannels_receiver Heq HŒ®". { iFrameSteps. }
      iIntros "!> H¬£ HŒ¶".

      wp_pures.
      iMod (lc_fupd_elim_later with "H¬£ Heq") as "Heq".
      iRewrite "Heq" in "HŒ®".
      iApply ("HŒ¶" $! (Some v)).
      iSteps.
  Qed.
  Lemma ws_queues_private_steal_to_spec t Œπ (sz : nat) i i_ ws j :
    i = ‚Å∫i_ ‚Üí
    (0 ‚â§ j < sz)%Z ‚Üí
    <<<
      ws_queues_private_inv t Œπ sz ‚àó
      ws_queues_private_owner t i_ Blocked ws
    | ‚àÄ‚àÄ vss,
      ws_queues_private_model t vss
    >>>
      ws_queues_private_steal_to t #i #j @ ‚ÜëŒπ
    <<<
      ‚àÉ‚àÉ o,
      match o with
      | None =>
          ws_queues_private_model t vss
      | Some v =>
          ‚àÉ vs,
          ‚åúvss !! ‚Çäj = Some (v :: vs)‚åù ‚àó
          ws_queues_private_model t (<[‚Çäj := vs]> vss)
      end
    | RET o;
      ws_queues_private_owner t i_ Blocked ws
    >>>.
  Proof.
    iIntros (-> Hj) "%Œ¶ ((:inv) & (:owner)) HŒ¶". injection Heq as <-.
    iDestruct (meta_agree with "Hmeta Hmeta_") as %<-. iClear "Hmeta_".
    opose proof* lookup_lt_Some; first done.

    wp_rec.
    iApply (wp_frame_wand with "[- Hchannels_sender Hchannels_receiver HŒ¶]"); first iAccu.
    wp_load.

    awp_apply (array_unsafe_get_spec_atomic_inv with "Hstatuses_inv") without "Hchannels_sender Hchannels_receiver HŒ¶"; first lia.
    iInv "Hinv" as "(:inv_inner =1)".
    iAaccIntro with "Hstatuses_model"; first iSteps.
    iIntros "%ùë†ùë°ùëéùë°ùë¢ùë† (%Hstatuses1_lookup & Hstatuses_model) !>".
    apply list_lookup_fmap_Some in Hstatuses1_lookup as (status & Hstatuses1_lookup & ->).
    iSplitL. { iFrameSteps. }
    iIntros "_ (Hchannels_sender & Hchannels_receiver & HŒ¶)".

    destruct status; wp_pures.

    - iMod "HŒ¶" as "(%vss & (:model) & _ & HŒ¶)". injection Heq as <-.
      iDestruct (meta_agree with "Hmeta Hmeta_") as %<-. iClear "Hmeta_".
      iMod ("HŒ¶" $! None with "[Hmodels_auth]") as "HŒ¶"; first iSteps.

      iSteps.

    - wp_load.

      awp_apply (array_unsafe_cas_spec_atomic_inv with "Hrequests_inv"); first lia.
      iInv "Hinv" as "(:inv_inner =2)".
      iAaccIntro with "Hrequests_model"; first iSteps.
      rewrite -(list_fmap_insert _ _ _ (RequestSome _)).
      iIntros "%b %ùëüùëíùëûùë¢ùëíùë†ùë° (%Hrequests2_lookup & %Hcas & Hrequests_model)".
      apply list_lookup_fmap_Some in Hrequests2_lookup as (request & Hrequests2_lookup & ->).
      destruct b.

      + destruct request; zoo_simplify in Hcas; first done.
        iMod (channels_prepare (Œª o, ws_queues_private_owner #l i_ Blocked ws -‚àó Œ¶ o)%I with "Hchannels_sender Hchannels_receiver") as "(Hchannels_sender & Hchannels_receiver)".
        iDestruct (big_sepL_insert_acc with "Hrequests") as "(Hrequest & Hrequests)"; first done.
        iDestruct ("Hrequests" $! (RequestSome i_) with "[Hrequest Hchannels_sender HŒ¶]") as "Hrequests".
        { iSteps.
          rewrite /request_au. iAuIntro.
          iApply (aacc_aupd_commit with "HŒ¶"); first done. iIntros "%vss (:model)". injection Heq as <-.
          iDestruct (meta_agree with "Hmeta Hmeta_") as %<-. iClear "Hmeta_".
          iAaccIntro with "Hmodels_auth"; first iSteps. iIntros (o) "Hmodels_auth".
          iExists o. iRevert "Hmodels_auth".
          destruct o; iSteps.
        }
        iSplitR "Hchannels_receiver". { iFrameSteps. }
        iIntros "!> _".

        wp_smart_apply (ws_queues_private_steal_to_0_spec with "[$Hmeta $Hl_responses $Hresponses_inv $Hinv $Hchannels_receiver]"); [lia.. |].
        iSteps.

      + iSplitR "Hchannels_sender Hchannels_receiver HŒ¶". { iFrameSteps. }
        iIntros "!> _".

        iMod "HŒ¶" as "(%vss & (:model) & _ & HŒ¶)". injection Heq as <-.
        iDestruct (meta_agree with "Hmeta Hmeta_") as %<-. iClear "Hmeta_".
        iMod ("HŒ¶" $! None with "[Hmodels_auth]") as "HŒ¶"; first iSteps.

        iSteps.
  Qed.
End ws_queues_private_G.

#[global] Opaque ws_queues_private_inv.
#[global] Opaque ws_queues_private_model.
#[global] Opaque ws_queues_private_owner.

Section ws_queues_private_G.
  Context `{ws_queues_private_G : WsQueuesPrivateG Œ£}.

  #[local] Lemma ws_queues_private_steal_as_0_spec t Œπ (sz : nat) i i_ ws round (n : nat) :
    i = ‚Å∫i_ ‚Üí
    <<<
      ws_queues_private_inv t Œπ sz ‚àó
      ws_queues_private_owner t i_ Blocked ws ‚àó
      random_round_model' round (sz - 1) n
    | ‚àÄ‚àÄ vss,
      ws_queues_private_model t vss
    >>>
      ws_queues_private_steal_as_0 t #sz #i round #n @ ‚ÜëŒπ
    <<<
      ‚àÉ‚àÉ o,
      match o with
      | None =>
          ws_queues_private_model t vss
      | Some v =>
          ‚àÉ j vs,
          ‚åú‚Çäi ‚â† j‚åù ‚àó
          ‚åúvss !! j = Some (v :: vs)‚åù ‚àó
          ws_queues_private_model t (<[j := vs]> vss)
      end
    | RET o;
      ‚àÉ n,
      ws_queues_private_owner t i_ Blocked ws ‚àó
      random_round_model' round (sz - 1) n
    >>>.
  Proof.
    iIntros (->) "%Œ¶ (#Hinv & Howner & Hround) HŒ¶".
    iDestruct (ws_queues_private_inv_owner with "Hinv Howner") as %Hi.

    iL√∂b as "HL√∂b" forall (n).

    wp_rec. wp_pures.
    case_bool_decide as Hcase; wp_pures.

    - iMod "HŒ¶" as "(%vss & Hmodel & _ & HŒ¶)".
      iApply ("HŒ¶" $! None with "Hmodel [$Howner Hround]"); first iSteps.

    - wp_apply (random_round_next_spec' with "Hround") as (j) "(%Hj & Hround)"; first lia.
      wp_pures.
      rewrite Nat2Z.id.
      pose k := (i_ + 1 + j) `mod` sz.
      assert ((i_ + 1 + j) `rem` sz = k)%Z as ->.
      { rewrite Z.rem_mod_nonneg; lia. }
      awp_smart_apply (ws_queues_private_steal_to_spec with "[$Hinv $Howner]") without "Hround"; [done | lia |].
      iApply (aacc_aupd with "HŒ¶"); first done. iIntros "%vss Hmodel".
      iAaccIntro with "Hmodel"; first iSteps. iIntros ([ v |]).

      + rewrite Nat2Z.id.
        iSteps as (vs Hlookup) "Hmodel". iExists (Some v). iSteps. iExists k. iSteps. iPureIntro.
        clear Hlookup. rewrite {}/k.
        destruct_decide (i_ + 1 + j < sz).
        * rewrite Nat.mod_small //. lia.
        * assert (i_ + 1 + j < sz * 2) as ?%Nat.div_lt_upper_bound by lia; last lia.
          assert ((i_ + 1 + j) `div` sz = 1) by lia.
          lia.

      + iSteps as "HŒ¶ Howner Hround".
        assert (n - 1 = (n - 1)%nat)%Z as -> by lia.
        iSteps.
  Qed.
  Lemma ws_queues_private_steal_as_spec t Œπ sz i i_ ws round :
    i = ‚Å∫i_ ‚Üí
    0 < sz ‚Üí
    <<<
      ws_queues_private_inv t Œπ sz ‚àó
      ws_queues_private_owner t i_ Blocked ws ‚àó
      random_round_model' round (sz - 1) (sz - 1)
    | ‚àÄ‚àÄ vss,
      ws_queues_private_model t vss
    >>>
      ws_queues_private_steal_as t #i round @ ‚ÜëŒπ
    <<<
      ‚àÉ‚àÉ o,
      match o with
      | None =>
          ws_queues_private_model t vss
      | Some v =>
          ‚àÉ j vs,
          ‚åú‚Çäi ‚â† j‚åù ‚àó
          ‚åúvss !! j = Some (v :: vs)‚åù ‚àó
          ws_queues_private_model t (<[j := vs]> vss)
      end
    | RET o;
      ‚àÉ n,
      ws_queues_private_owner t i_ Blocked ws ‚àó
      random_round_model' round (sz - 1) n
    >>>.
  Proof.
    iIntros (->) "%Hsz %Œ¶ (#Hinv & Hround) HŒ¶".

    wp_rec.
    wp_smart_apply (ws_queues_private_size_spec with "Hinv") as "_".
    wp_pures.
    assert (sz - 1 = (sz - 1)%nat)%Z as -> by lia.
    wp_apply (ws_queues_private_steal_as_0_spec with "[$Hinv $Hround] HŒ¶"); first done.
  Qed.
End ws_queues_private_G.

From zoo_parabs Require
  ws_queues_private__opaque.
